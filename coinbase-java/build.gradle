buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
    }
}

def packageName = 'coinbase-java'
def packageVersion = '2.0.0-28'
def packageDescription = 'Coinbase Android SDK'
group = 'com.coinbase'
version = packageVersion

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName packageVersion
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    exclude { "com/coinbase/v1/localServerFiles/**" }
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name packageName
            description packageDescription
            url 'https://github.com/coinbase/coinbase-java.git'
            inceptionYear '2015'

            packaging 'aar'
            groupId 'com.coinbase'
            artifactId packageName
            version packageVersion

            licenses {
                license {
                    name 'MIT'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/coinbase/coinbase-java.git'
                url 'https://github.com/coinbase/coinbase-java'

            }
            developers {
                developer {
                    name 'Coinbase'
                }
            }
        }
    }
}

bintray {
    user = project.property('bintrayUser')
    key = project.property('bintrayKey')

    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = packageName
        userOrg = 'coinbase'
        desc = packageDescription
        licenses = ['MIT']
        vcsUrl = 'https://github.com/coinbase/coinbase-java.git'
        version {
            name = packageVersion
            desc = 'Coinbase Android SDK'
            released = new Date()
            vcsTag = packageVersion
        }
    }
}


dependencies {
    compile 'com.google.code.gson:gson:2.5'
    compile 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'org.joda:joda-money:0.11'
    compile 'joda-time:joda-time:2.3'
    compile 'net.sf.opencsv:opencsv:2.0'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.4.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.0'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-codec:commons-codec:1.10'
    testCompile 'junit:junit:4.12'
    testCompile "org.powermock:powermock-module-junit4:1.6.2"
    testCompile "org.powermock:powermock-module-junit4-rule:1.6.2"
    testCompile "org.powermock:powermock-api-mockito:1.6.2"
    testCompile "org.powermock:powermock-classloading-xstream:1.6.2"
}